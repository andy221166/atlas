plugins {
    id 'java-library'
    id 'org.springframework.boot' version "${springBootPluginVersion}"
    id 'io.spring.dependency-management' version "${springDependencyManagementPluginVersion}"
    id 'idea'
}

group = 'org.atlas'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Lib
    api 'org.springframework.boot:spring-boot-starter'
    api "software.amazon.awssdk:sns:${awsSdkVersion}"
    api "software.amazon.awssdk:sqs:${awsSdkVersion}"
    api 'org.apache.httpcomponents:httpclient:4.5.14'

    // Infrastructure
    switch (infraStack['persistence']) {
        case 'jpa':
            api project(':infrastructure.spring-boot.persistence.jpa.adapter.outbox')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${infraStack['persistence']}")
    }

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}

test {
    useJUnitPlatform()
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}
