plugins {
    id 'java-library'
    id 'org.springframework.boot' version "${springBootPluginVersion}"
    id 'io.spring.dependency-management' version "${springDependencyManagementPluginVersion}"
    id 'idea'
}

group = 'org.atlas'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Lib
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-server'

    // Infrastructure
    api project(':infrastructure.spring-boot.bootstrap')
    switch (infraStack['config']) {
        case 'yaml':
            api project(':infrastructure.spring-boot.config.yaml')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${infraStack['config']}")
    }
    api project(':infrastructure.spring-boot.observability.actuator')
    switch (infraStack['observability.logging']) {
        case 'logback':
            api project(':infrastructure.spring-boot.observability.logging.logback')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${infraStack['observability.logging']}")
    }
    switch (infraStack['observability.metrics']) {
        case 'none':
            break;
        case 'prometheus':
            api project(':infrastructure.spring-boot.observability.metrics.prometheus')
            break
        case 'cloudwatch':
            api project(':infrastructure.spring-boot.observability.metrics.cloudwatch')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${infraStack['observability.metrics']}")
    }

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

test {
    useJUnitPlatform()
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}
