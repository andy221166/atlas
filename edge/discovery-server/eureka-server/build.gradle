plugins {
    id 'maven-publish'
    id 'org.springframework.boot' version '3.4.0'
}

def config = rootProject.ext.loadProfileConfig()

dependencies {
    api 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-server'

    switch (config['logging']) {
        case 'logback':
            api project(':logging-logback')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${config['logging']}")
    }

    api project(':monitoring')

    switch (config['tracing']) {
        case 'none':
            break
        case 'zipkin':
            api project(':tracing-zipkin')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${config['tracing']}")
    }
}

jar {
    enabled = false
}

publishing {
    publications {
        publication(MavenPublication) {
            artifact bootJar
            from components.java
        }
    }
}
