plugins {
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'maven-publish'
}

dependencies {
    // Lib
    implementation 'org.springframework.cloud:spring-cloud-starter-gateway'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springdoc:springdoc-openapi-starter-webflux-ui:2.8.6'

    // Infrastructure
    api project(':infrastructure-spring-boot:actuator')
    api project(':infrastructure-spring-boot:config')
    switch (stackConfig['discovery-client']) {
        case 'none':
            break
        case 'eureka':
            api project(':infrastructure-spring-boot:discovery-client-eureka')
            break
        case 'kubernetes':
            api project(':infrastructure-spring-boot:discovery-client-kubernetes')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${stackConfig['discovery-client']}")
    }
    switch (stackConfig['logging']) {
        case 'logback':
            api project(':infrastructure-spring-boot:logging-logback')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${stackConfig['logging']}")
    }
    switch (stackConfig['redis']) {
        case 'standalone':
            api project(':infrastructure-spring-boot:redis-standalone')
            break
        case 'cluster':
            api project(':infrastructure-spring-boot:redis-cluster')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${stackConfig['redis']}")
    }
    switch (stackConfig['tracing']) {
        case 'simple':
            api project(':infrastructure-spring-boot:tracing-simple')
            break
        case 'zipkin':
            api project(':infrastructure-spring-boot:tracing-zipkin')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${stackConfig['tracing']}")
    }

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

jar {
    enabled = false
}

publishing {
    publications {
        publication(MavenPublication) {
            artifact bootJar
            from components.java
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
