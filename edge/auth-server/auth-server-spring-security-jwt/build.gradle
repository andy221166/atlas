plugins {
    id 'java-library'
    id 'org.springframework.boot' version "${springBootPluginVersion}"
    id 'io.spring.dependency-management' version "${springDependencyManagementPluginVersion}"
}

group = 'org.atlas'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Lib
    api 'org.springframework.boot:spring-boot-starter-security'

    // Infrastructure
    api project(':infrastructure.spring-boot.api-server.rest.core')
    api project(':infrastructure.spring-boot.bootstrap')
    api project(':infrastructure.spring-boot.cache.simple')
    switch (infraStack['config']) {
        case 'yaml':
            api project(':infrastructure.spring-boot.config.yaml')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${infraStack['config']}")
    }
    switch (infraStack['datasource']) {
        case 'mysql':
            api project(':infrastructure.spring-boot.datasource.mysql.adapter.auth')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${infraStack['datasource']}")
    }
    switch (infraStack['discovery-client']) {
        case 'none':
            break
        case 'eureka':
            api project(':infrastructure.spring-boot.discovery-client.eureka')
            break
        case 'kubernetes':
            api project(':infrastructure.spring-boot.discovery-client.kubernetes')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${infraStack['discovery-client']}")
    }
    api project(':infrastructure.spring-boot.observability.actuator')
    switch (infraStack['observability.logging']) {
        case 'logback':
            api project(':infrastructure.spring-boot.observability.logging.logback')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${infraStack['observability.logging']}")
    }
    switch (infraStack['observability.metrics']) {
        case 'none':
            break;
        case 'prometheus':
            api project(':infrastructure.spring-boot.observability.metrics.prometheus')
            break
        case 'cloudwatch':
            api project(':infrastructure.spring-boot.observability.metrics.cloudwatch')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${infraStack['observability.metrics']}")
    }
    switch (infraStack['observability.tracing']) {
        case 'simple':
            api project(':infrastructure.spring-boot.observability.tracing.simple')
            break
        case 'zipkin':
            api project(':infrastructure.spring-boot.observability.tracing.zipkin')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${infraStack['observability.tracing']}")
    }
    switch (infraStack['persistence']) {
        case 'jpa':
            api project(':infrastructure.spring-boot.persistence.jpa.adapter.auth')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${infraStack['persistence']}")
    }
    switch (infraStack['redis']) {
        case 'standalone':
            api project(':infrastructure.spring-boot.redis.standalone')
            break
        case 'cluster':
            api project(':infrastructure.spring-boot.redis.cluster')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${infraStack['redis']}")
    }

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

test {
    useJUnitPlatform()
}
