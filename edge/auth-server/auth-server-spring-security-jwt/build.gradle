plugins {
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'maven-publish'
}

dependencies {
    // Lib
    api 'org.springframework.boot:spring-boot-starter-security'

    // Infrastructure
    api project(':infrastructure-spring-boot:api-server-rest-core')
    api project(':infrastructure-spring-boot:cache-simple')
    api project(':infrastructure-spring-boot:config')
    switch (stackConfig['datasource']) {
        case 'mysql':
            api project(':infrastructure-spring-boot:datasource-mysql-adapter-auth')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${stackConfig['datasource']}")
    }
    switch (stackConfig['persistence']) {
        case 'jpa':
            api project(':infrastructure-spring-boot:persistence-jpa-adapter-auth')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${stackConfig['persistence']}")
    }
    switch (stackConfig['redis']) {
        case 'standalone':
            api project(':infrastructure-spring-boot:redis-standalone')
            break
        case 'cluster':
            api project(':infrastructure-spring-boot:redis-cluster')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${stackConfig['redis']}")
    }
}

jar {
    enabled = false
}

publishing {
    publications {
        publication(MavenPublication) {
            artifact bootJar
            from components.java
        }
    }
}
