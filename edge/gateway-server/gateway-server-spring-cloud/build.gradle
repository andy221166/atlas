plugins {
    id 'maven-publish'
    id 'org.springframework.boot' version '3.4.0'
}

def modules = rootProject.ext.loadStackConfig()

dependencies {
    api project(':commons')
    api project(':config-loader')

    switch (modules['discovery-client']) {
        case 'none':
            break
        case 'eureka':
            api project(':eureka-client')
            break
        case 'kubernetes':
            api project(':kubernetes-client')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${modules['discovery-client']}")
    }

    switch (modules['logging']) {
        case 'logback':
            api project(':logging-logback')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${modules['logging']}")
    }

    api project(':actuator')

    switch (modules['tracing']) {
        case 'none':
            break
        case 'zipkin':
            api project(':tracing-zipkin')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${modules['tracing']}")
    }

    api 'org.springframework.cloud:spring-cloud-starter-gateway'
    api 'com.auth0:java-jwt:4.4.0'
}

jar {
    enabled = false
}

publishing {
    publications {
        publication(MavenPublication) {
            artifact bootJar
            from components.java
        }
    }
}
