plugins {
    id 'maven-publish'
    id 'org.springframework.boot' version '3.4.0'
}

def modules = rootProject.ext.loadStackConfig()

dependencies {
    // Lib
    api 'org.springframework.cloud:spring-cloud-starter-gateway'

    // Platform
    api project(':actuator')
    api project(':commons')
    api project(':config')
    switch (modules['discovery-client']) {
        case 'none':
            break
        case 'eureka':
            api project(':discovery-client-eureka')
            break
        case 'kubernetes':
            api project(':discovery-client-kubernetes')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${modules['discovery-client']}")
    }
    api project(':jwt-auth0')
    switch (modules['logging']) {
        case 'logback':
            api project(':logging-logback')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${modules['logging']}")
    }
    switch (modules['tracing']) {
        case 'none':
            break
        case 'zipkin':
            api project(':tracing-zipkin')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${modules['tracing']}")
    }
}

jar {
    enabled = false
}

publishing {
    publications {
        publication(MavenPublication) {
            artifact bootJar
            from components.java
        }
    }
}
