plugins {
    id 'maven-publish'
    id 'org.springframework.boot' version '3.4.0'
}

def modules = rootProject.ext.loadStackConfig()

dependencies {
    api project(':config-loader')
    api project(':task-contract')

    switch (modules['datasource']) {
        case 'mysql':
            api project(':mysql-task')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${modules['datasource']}")
    }

    switch (modules['logging']) {
        case 'logback':
            api project(':logging-logback')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${modules['logging']}")
    }

    api project(':monitoring')

    switch (modules['orm']) {
        case 'jdbc':
            api project(':jdbc-task')
            break
        case 'jpa':
            api project(':jpa-task')
            break
        case 'mybatis':
            api project(':mybatis-task')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${modules['orm']}")
    }

    api project(':task-scheduler-quartz')

    switch (modules['tracing']) {
        case 'none':
            break
        case 'zipkin':
            api project(':tracing-zipkin')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${modules['tracing']}")
    }
}

jar {
    enabled = false
}

publishing {
    publications {
        publication(MavenPublication) {
            artifact bootJar
            from components.java
        }
    }
}
