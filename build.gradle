plugins {
	// Ensures all subprojects are treated as Java libraries with api and implementation configurations
	id 'java'
	id 'java-library'
	// Allows subprojects to use BOMs (e.g., Spring Boot or Protobuf), centralizing dependency versions
	id 'io.spring.dependency-management' version '1.1.6'
	// Enhances IntelliJ IDEA integration, which is useful for developers using that IDE
	id 'idea'
}

ext {
	set('springBootVersion', '3.4.0')
	set('springCloudVersion', '2024.0.0')
	set('lombokVersion', '1.18.38') // Define lombokVersion here for global access
}

subprojects {
	version = '0.0.1-SNAPSHOT'

	apply plugin: 'java'
	apply plugin: 'java-library'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'idea'
	apply from: "$rootDir/gradle/config/config-loader.gradle"

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(JavaVersion.VERSION_17 as String)
		}
	}

	configurations { // Lombok support
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	repositories {
		mavenLocal()
		mavenCentral()
	}

	dependencies {
		// Lombok
		annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
		testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
		testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
	}

	idea {
		module {
			downloadJavadoc = true
			downloadSources = true
		}
	}
}
