plugins {
    id 'java-library'
    id 'org.springframework.boot' version "${springBootPluginVersion}"
    id 'io.spring.dependency-management' version "${springDependencyManagementPluginVersion}"
    id 'idea'
}

group = 'org.atlas'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Domain
    api project(':domain.order')

    // Infrastructure
    switch (infraStack['api-client']) {
        case 'grpc-netdevh':
            api project(':infrastructure.spring-boot.internal-api.product.grpc.netdevh')
            api project(':infrastructure.spring-boot.internal-api.user.grpc.netdevh')
            break
        case 'rest-apachehttpclient':
            api project(':infrastructure.spring-boot.internal-api.product.rest.apachehttpclient')
            api project(':infrastructure.spring-boot.internal-api.user.rest.apachehttpclient')
            break
        case 'rest-feign':
            api project(':infrastructure.spring-boot.internal-api.product.rest.feign')
            api project(':infrastructure.spring-boot.internal-api.user.rest.feign')
            break
        case 'rest-resttemplate':
            api project(':infrastructure.spring-boot.internal-api.product.rest.resttemplate')
            api project(':infrastructure.spring-boot.internal-api.user.rest.resttemplate')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${infraStack['api-client']}")
    }
    switch (infraStack['api-server']) {
        case 'rest':
            api project(':infrastructure.spring-boot.api-server.rest.adapter.order')
            break
        case 'grpc':
            api project(':infrastructure.spring-boot.api-server.grpc.netdevh.adapter.order')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${infraStack['api-server']}")
    }
    api project(':infrastructure.spring-boot.bootstrap')
    switch (infraStack['config']) {
        case 'yaml':
            api project(':infrastructure.spring-boot.config.yaml')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${infraStack['config']}")
    }
    switch (infraStack['datasource']) {
        case 'mysql':
            api project(':infrastructure.spring-boot.datasource.mysql.adapter.order')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${infraStack['datasource']}")
    }
    switch (infraStack['discovery-client']) {
        case 'none':
            break
        case 'eureka':
            api project(':infrastructure.spring-boot.discovery-client.eureka')
            break
        case 'kubernetes':
            api project(':infrastructure.spring-boot.discovery-client.kubernetes')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${infraStack['discovery-client']}")
    }
    switch (infraStack['messaging']) {
        case 'kafka':
            api project(':infrastructure.spring-boot.messaging.kafka.adapter.order')
            break
        case 'sns':
            api project(':infrastructure.spring-boot.messaging.sns.adapter.order')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${infraStack['messaging']}")
    }
    api project(':infrastructure.spring-boot.event-handler-decorator')
    api project(':infrastructure.spring-boot.observability.actuator')
    switch (infraStack['observability.logging']) {
        case 'logback':
            api project(':infrastructure.spring-boot.observability.logging.logback')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${infraStack['observability.logging']}")
    }
    switch (infraStack['observability.metrics']) {
        case 'none':
            break;
        case 'prometheus':
            api project(':infrastructure.spring-boot.observability.metrics.prometheus')
            break
        case 'cloudwatch':
            api project(':infrastructure.spring-boot.observability.metrics.cloudwatch')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${infraStack['observability.metrics']}")
    }
    switch (infraStack['observability.tracing']) {
        case 'simple':
            api project(':infrastructure.spring-boot.observability.tracing.simple')
            break
        case 'zipkin':
            api project(':infrastructure.spring-boot.observability.tracing.zipkin')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${infraStack['observability.tracing']}")
    }
    switch (infraStack['persistence']) {
        case 'jpa':
            api project(':infrastructure.spring-boot.persistence.jpa.adapter.order')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${infraStack['persistence']}")
    }
    switch (infraStack['scheduler']) {
        case 'spring':
            api project(':infrastructure.spring-boot.scheduler.spring.adapter.order')
            break
        case 'quartz':
            api project(':infrastructure.spring-boot.scheduler.quartz.adapter.order')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${infraStack['scheduler']}")
    }
    api project(':infrastructure.spring-boot.sequence-generator')
    api project(':infrastructure.spring-boot.use-case-handler-decorator')

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

test {
    useJUnitPlatform()
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}
