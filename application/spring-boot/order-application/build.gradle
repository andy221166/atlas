plugins {
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'maven-publish'
}

def modules = loadStackConfig()

dependencies {
    // Port
    api project(':order-port-inbound')
    api project(':order-port-outbound')

    // Platform
    api project(':adapter-spring-boot:actuator')
    switch (modules['api-client']) {
        case 'grpc-netdevh':
            api project(':adapter-spring-boot:product-api-client-grpc-netdevh')
            api project(':adapter-spring-boot:user-api-client-grpc-netdevh')
            break
        case 'rest-apachehttpclient':
            api project(':adapter-spring-boot:product-api-client-rest-apachehttpclient')
            api project(':adapter-spring-boot:user-api-client-rest-apachehttpclient')
            break
        case 'rest-feign':
            api project(':adapter-spring-boot:product-api-client-rest-feign')
            api project(':adapter-spring-boot:user-api-client-rest-feign')
            break
        case 'rest-resttemplate':
            api project(':adapter-spring-boot:product-api-client-rest-resttemplate')
            api project(':adapter-spring-boot:user-api-client-rest-resttemplate')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${modules['api-client']}")
    }
    switch (modules['api-server']) {
        case 'rest':
            api project(':adapter-spring-boot:order-api-server-rest')
            break
        case 'grpc':
            api project(':adapter-spring-boot:order-api-server-grpc-netdevh')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${modules['api-server']}")
    }
    api project(':adapter-spring-boot:config')
    switch (modules['datasource']) {
        case 'mysql':
            api project(':adapter-spring-boot:order-datasource-mysql')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${modules['datasource']}")
    }
    switch (modules['discovery-client']) {
        case 'none':
            break
        case 'eureka':
            api project(':adapter-spring-boot:discovery-client-eureka')
            break
        case 'kubernetes':
            api project(':adapter-spring-boot:discovery-client-kubernetes')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${modules['discovery-client']}")
    }

    switch (modules['event']) {
        case 'kafka':
            api project(':adapter-spring-boot:order-event-kafka')
            break
        case 'sns':
            api project(':adapter-spring-boot:order-event-sns')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${modules['event']}")
    }
    switch (modules['logging']) {
        case 'logback':
            api project(':adapter-spring-boot:logging-logback')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${modules['logging']}")
    }
    switch (modules['persistence']) {
        case 'jpa':
            api project(':adapter-spring-boot:order-persistence-jpa')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${modules['persistence']}")
    }
    api project(':adapter-spring-boot:sequence-generator')
    switch (modules['tracing']) {
        case 'none':
            break
        case 'zipkin':
            api project(':adapter-spring-boot:tracing-zipkin')
            break
        default:
            throw new IllegalArgumentException("Unsupported ${modules['tracing']}")
    }
}

jar {
    enabled = false
}

publishing {
    publications {
        publication(MavenPublication) {
            artifact bootJar
            from components.java
        }
    }
}
